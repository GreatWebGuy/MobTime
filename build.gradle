import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
plugins {
    id 'application'
    id 'org.beryx.jlink'                 version '3.0.1'
}

def currentPlatform = new DefaultNativePlatform("current")
def currentOS = currentPlatform.getOperatingSystem()
String currentArch = currentPlatform.getArchitecture().getName()
String platform
String fileName
if (currentOS.isWindows()) {
    platform = 'win'
    fileName = "mobtime-windows-" + currentArch
} else if (currentOS.isLinux()) {
    platform = 'linux'
    fileName = "mobtime-linux"
} else if (currentOS.isMacOsX()) {
    fileName = "mobtime-macos-" + currentArch
    if (currentArch == 'aarch64') {
        platform = 'mac-' + currentArch
    } else {
        platform = 'mac'
    }
}

application {
    mainModule = 'com.greatwebguy.application'
    mainClass = 'com.greatwebguy.application.MobTime'
}

repositories {
    mavenCentral()
}

java {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

dependencies {
    implementation "org.openjfx:javafx-base:21.0.1:${platform}"
    implementation "org.openjfx:javafx-controls:21.0.1:${platform}"
    implementation "org.openjfx:javafx-graphics:21.0.1:${platform}"
    implementation "org.openjfx:javafx-fxml:21.0.1:${platform}"
    implementation "org.openjfx:javafx-media:21.0.1:${platform}"
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "MobTime"
    }
    jpackage {
        installerName = "${fileName}"
        installerOptions = [
            '--description', 'Mob Timer for Mob Programming',
            '--copyright', 'Copyright 2023 GreatWebGuy',
            '--app-version', '1.7.8'
        ]
        installerType = project.findProperty('installerType')
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/deploy/package/windows/MobTime.ico']
            installerOptions += [
                '--win-per-user-install', '--win-dir-chooser',
                '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/deploy/package/macosx/MobTime.icns']
        }
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'src/main/deploy/package/default/icon_256x256.png']
            installerOptions += [
                '--linux-menu-group', 'Office',
                '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                '--linux-deb-maintainer', 'webguy@greatwebguy.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                '--linux-rpm-license-type', 'GPLv3'
            ]
        }
    }
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}

plugins {
    id 'application'
    id 'org.beryx.jlink'                 version '2.24.4'
}

def currentOS = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem
String platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

application {
    mainModule = 'com.greatwebguy.application'
    mainClass = 'com.greatwebguy.application.MobTime'
}

repositories {
    mavenCentral()
    jcenter()
}

java {
    modularity.inferModulePath = true
}

dependencies {
    implementation "org.openjfx:javafx-base:17.0.1:${platform}"
    implementation "org.openjfx:javafx-controls:17.0.1:${platform}"
    implementation "org.openjfx:javafx-graphics:17.0.1:${platform}"
    implementation "org.openjfx:javafx-fxml:17.0.1:${platform}"
    implementation "org.openjfx:javafx-media:17.0.1:${platform}"
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.2.0'
}

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'MobTime'
    }
    jpackage {
        installerOptions = [
            '--description', 'Mob Timer for Mob Programming',
            '--copyright', 'Copyright 2021 GreatWebGuy',
            '--app-version', '1.7.7'
        ]
        installerType = project.findProperty('installerType')
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/deploy/package/windows/MobTime.ico']
            installerOptions += [
                '--win-per-user-install', '--win-dir-chooser',
                '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/deploy/package/macosx/MobTime.icns']
        }
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'src/main/deploy/package/default/icon_256x256.png']
            installerOptions += [
                '--linux-menu-group', 'Office',
                '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                '--linux-deb-maintainer', 'webguy@greatwebguy.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                '--linux-rpm-license-type', 'GPLv3'
            ]
        }
    }
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}
